
def xbibGroup = 'org.xbib'
def xbibVersion = '2.0'
def xbibBuildNumber = System.getenv("BUILD_NUMBER") as Integer ?: 0

group = xbibGroup
version = xbibVersion + '.' + xbibBuildNumber + '-SNAPSHOT'

println "Current JVM: " + org.gradle.internal.jvm.Jvm.current()
println "Build: ${project.group} ${project.name} ${project.version}"

ext {
  versions = [
    'jackson': '2.6.4',
    'netty': '4.0.33.Final',
    'woodstox' : '5.0.2',
    'log4j': '2.5',
    'junit' : '4.12',
    'gradle-plugin-jflex': '1.0.0',
    'gradle-plugin-jacc': '1.0.3',
    'xbib-elasticsearch-helper' : '2.1.1.4'
  ]
}

allprojects {
    apply plugin: 'maven'
    group = xbibGroup
    version = xbibVersion + '.' + xbibBuildNumber + '-SNAPSHOT'
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            url "http://xbib.org/repository"
        }
    }
    configurations {
        wagon
    }
    dependencies {
        wagon 'org.apache.maven.wagon:wagon-ssh-external:2.10'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    configurations {
        provided
        testCompile.extendsFrom(provided)
    }
    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
    dependencies {
        compile "org.apache.logging.log4j:log4j-core:${versions.log4j}"
        provided "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}"
        testCompile "junit:junit:${versions.junit}"
    }
    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    // TODO fix java compile warnings by enabling this
    //tasks.withType(JavaCompile) {
    //    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    //}
    test {
        classpath += configurations.provided
        testLogging {
            showStandardStreams = false
            exceptionFormat = 'full'
        }
    }
    tasks.withType(FindBugs) {
        ignoreFailures = true
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }
    artifacts {
        archives sourcesJar
    }
    uploadArchives {
        repositories {
            if (project.hasProperty("xbibUsername")) {
                mavenDeployer {
                    configuration = configurations.wagon
                    repository(
                            id: 'xbib.org',
                            url: uri('scpexe://xbib.org/repository'),
                            authentication: [userName: xbibUsername, privateKey: xbibPrivateKey]
                    )
                }
            }
        }
        dependsOn {
            rootProject.subprojects.collect {
                it.tasks.getByName 'test'
            }
        }
    }
}

