
plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

configurations {
    alpnboot
}

dependencies {
    compile project(':time')
    compile project(':matching')
    compile project(':transformer')
    compile project(':content:xml')
    compile project(':content:csv')
    compile project(':oai')
    compile project(':sru')
    compile project(':io:charset')
    compile project(':io:archive')
    compile project(':io:jdbc')
    compile project(':graphics:chart')
    compile project(':datastructures:forkjoin')
    compile "org.xbib.elasticsearch.plugin:elasticsearch-helper:${versions.'xbib-elasticsearch'}"
    compile "xerces:xercesImpl:2.11.0"
    compile "xalan:xalan:2.7.2"
    compile "com.fasterxml.woodstox:woodstox-core:${versions.woodstox}"
    compile 'org.apache.logging.log4j:log4j-1.2-api:' + versions.log4j
    // openssl native libs are optional and must be added separately
    provided "org.javassist:javassist:3.20.0-GA"
    if ('os x' == org.gradle.internal.os.OperatingSystem.current().getFamilyName()) {
        provided "io.netty:netty-tcnative:1.1.33.Fork16:osx-x86_64"
    }
    if ('linux' == org.gradle.internal.os.OperatingSystem.current().getFamilyName()) {
        if (new File("/etc/redhat-release").exists()) {
            // use this for linking to libssl.so.10 (RHEL/Fedora/CentOS)
            provided "io.netty:netty-tcnative:1.1.33.Fork16:linux-x86_64-fedora"
        } else {
            // use this for linking to libssl.so.1.0.0
            provided "io.netty:netty-tcnative:1.1.33.Fork16:linux-x86_64"
        }
    }
    provided project(':io:charset') // for bibliographic charsets
    alpnboot 'org.mortbay.jetty.alpn:alpn-boot:8.1.8.v20160420'
}

test {
    useJUnit {
        excludeCategories 'org.xbib.RuntimeTests'
        systemProperty 'RESOURCES_DIR', "${buildDir}/resources/test"
        testLogging {
            showStandardStreams = false
            exceptionFormat = 'full'
        }
    }
}

task runnerTest(type: Test) {
    useJUnit {
        forkEvery 1
        includeCategories 'org.xbib.RuntimeTests'
        testLogging {
            showStandardStreams = false
            exceptionFormat = 'full'
        }
    }
}

shadowJar {
    baseName = 'xbib-' + project.name
    version = project.version
    classifier = 'standalone'
    manifest {
        attributes 'Main-Class': 'org.xbib.tools.Runner'
    }
}

artifacts {
    archives shadowJar
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = 'xbib-' + project.name
        }
    }
}

clean {
    delete 'logs'
}

apply from: 'gradle/xbib.gradle'